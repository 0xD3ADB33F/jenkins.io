/*
 * This build.gradle file is to be used for generating the prototype
 * jenkins.io, via static generation
 */
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.ajoberstar:gradle-git:1.2.0"
        classpath "com.github.jruby-gradle:jruby-gradle-plugin:[1.1.4,2.0)"
    }
}
apply plugin: 'org.ajoberstar.github-pages'
apply plugin: 'com.github.jruby-gradle.base'

description = 'Build for generating jenkins.io'
defaultTasks 'assemble'

configurations {
    asciidoctor
    jekyllImport

    externalFetcher
}

jruby {
    defaultVersion jrubyVersion
    execVersion jrubyVersion
}

dependencies {
    /* used for primary site generation */
    asciidoctor 'rubygems:awestruct:[0.5.6,0.6)'
    /* ensure we pull in a more recent version of asciidoctor */
    asciidoctor 'rubygems:asciidoctor:[1.5.3,2.0)'
    /* used for legacy markdown template processing via tilt */
    asciidoctor 'rubygems:kramdown:[1.9.0,2.0)'

    /* used for processing haml templates (haml.info) */
    asciidoctor 'rubygems:haml:[4.0.7,5.0)'
    /* used for processing inline liquid templating */
    asciidoctor 'rubygems:liquid:[3.0.6,4.0)'

    jekyllImport 'rubygems:sequel:[4.27.0,5.0)'
    jekyllImport 'org.postgresql:postgresql:9.3-1104-jdbc41'


    /* needed for clean HTTP accesses */
    externalFetcher 'rubygems:faraday:[0.9.2,1.0)'
}

import com.github.jrubygradle.JRubyExec

task fetchExternalResources(type: JRubyExec) {
    group 'Site'
    description 'Fetch external resources which need to be compiled into the site'
    script "${projectDir}/scripts/fetch-external-resources"
    configuration 'externalFetcher'
    inputs.source file("${projectDir}/scripts/fetch-external-resources")
}

task copyLegacyAssets(type: Copy) {
    group 'Site'
    description 'Copy legacy Drupal assets to preserve legacy content rendering'
    File targetDir = file("${buildDir}/_site/sites/default/files")
    targetDir.mkdirs()

    destinationDir = targetDir
    from "${projectDir}/legacy/files"
}

task compileContent(type: JRubyExec) {
    group 'Site'
    description 'Compile the site using the awestruct CLI'
    dependsOn fetchExternalResources, copyLegacyAssets
    script 'awestruct'
    scriptArgs '--generate', '--verbose',
                '--source-dir', "${projectDir}/${contentDir}",
                '--output-dir', "${buildDir}/_site"
    /* without the --force option, awestruct is not smart enough to regenerate
     * files based on includes */
    configuration 'asciidoctor'
    /* <https://github.com/awestruct/awestruct/issues/497> */
    workingDir contentDir
    inputs.source fileTree(projectDir)
                    .include("${contentDir}/**/*.adoc")
                    .include("${contentDir}/**/*.haml")
                    .include("${contentDir}/**/*.md")
}

task assemble {
    group 'Site'
    description 'Assemble the full site'
    dependsOn fetchExternalResources, compileContent
}

githubPages {
    repoUri = 'git@github.com:jenkinsci/jenkins.io.git'
    targetBranch = 'gh-pages'
    pages {
        from "${buildDir}/_site"
        from "${projectDir}/CNAME"
    }
}
publishGhPages.dependsOn assemble

task importFromDrupal(type: JRubyExec) {
    group 'Other'
    description 'Import the legacy Drupal site directly from a DB'
    script "${projectDir}/scripts/drupal-import"
    scriptArgs drupalDatabase, drupalUsername, drupalPassword
    configuration 'jekyllImport'
    workingDir "${projectDir}/${contentDir}"
    inputs.source file("${projectDir}/scripts/drupal-import")
}
