---
layout: section
---
:notitle:
:description:
:author:
:email: jenkinsci-docs@googlegroups.com
:sectanchors:
:toc:
:imagesdir: /doc/book/resources
:hide-uri-scheme:

= Pipeline DSL reference

This section covers the syntax for Declarative Pipeline. 

[[pipeline]]
== pipeline

* _Description_: Top level step, containing a closure with the model
configuration inside it.
* _Parameters_: none
* _Takes a Closure_: Yes.
* _Closure Contents_: See below.

[[within-pipeline]]
== Within `pipeline`

These are sections that are specified directly within the `pipeline`
argument closure.

[[agent]]
=== agent

* _Description_: Specifies where the build or stage will run.
* _Required_: Yes for the top-level `pipeline` closure, optional for
individual `stage` closures.
* _Allowed In_: Top-level `pipeline` closure and individual `stage`
closures.
* _Parameters_: Either a `Closure` of one key and either a single value
or a `Closure` of multiple configuration pairs, or one of two constants
- `none` or `any`.
** _Map Keys_:
*** Note that this will be an `ExtensionPoint`, so plugins will be able
to add to the available image providers.
*** `docker`
**** _Description_: If given, uses this Docker image for the container
the build will run in. If no `label` is given, the container will be run
within a simple `node { ... }` block.
*** `dockerfile`
**** _Description_: If given, builds from the Dockerfile in the
repository and runs in a container using the resulting image.
*** `label`
**** _Description_: If given, uses this label for the node the build
will run on - if `docker` is also specified, the container will run on
that node.
** `none`
*** _Type_: bareword
*** _Description_: If given, node/image management will need to be
specified explicitly in stages and no automatic `checkout scm` call will
occur.
** `any`
*** _Type_: bareword
*** _Description_: If given, any available agent will be used.
* _Takes a Closure_: yes
* _Examples_:

[pipeline]
----
// Declarative //
agent {
    label 'hi-speed'
}
----
[pipeline]
----
agent {
    docker 'ubuntu:lts'
}
----
[pipeline]
----
// Declarative //
agent {
    docker {
        image 'ubuntu:lts'
        label 'has-docker'
        args "-v /tmp:/tmp -p 80:80"
    }
}
----
[pipeline]
----
// Declarative //
agent none
----
[pipeline]
----
// Declarative //
agent any
----

[[environment]]
=== environment

* _Description_: A sequence of `key = value` pairs, which will be passed
to the `withEnv` step the build will be executed within.
* _Required_: No
* _Allowed In_: Top-level `pipeline` or `stage` closures only.
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: One or more lines with `foo = 'bar'` variable
name/value pairs.
** The name doesn't need to be quoted.
** Bind credentials using the "credentials('<id>')" function. _NOTE_:
credentials binding requires that the pipeline or stage is running
within an agent. In the example below the credentials gets bound in
slightly different ways depending of the type of the key;
*** If it is a `Secret Text` the variable `SAUCE_ACCESS` will contain
that text.
*** If it is a `Secret File` the variable `SAUCE_ACCESS` will contain a
path to the file on the build agent.
*** If it is a `Standard username and password` credential, three
variables will be added to the environment:
**** `SAUCE_ACCESS` containing '<username>:<password>'
**** `SAUCE_ACCESS_USR` containing the username
**** `SAUCE_ACCESS_PSW` containing the password
* _Examples_:

[pipeline]
----
// Declarative //
environment {
   CXX          = 'g++-4.8'
   SAUCE_ACCESS = credentials('sauce-lab-dev')
   someVar      = 'someValue'
}
----

[[stages]]
=== stages

* _Description_: A sequence of one or more Pipeline `stage`, each of
which consists of a sequence of steps.
* _Required_: Yes
* _Allowed In_: Top-level `pipeline` closure only.
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: one or more `stage` blocks, as described below.

[[stage]]
=== stage

* _Description_: A block for a single `stage`, containing a sequence of
steps. Note that this syntax matches up with the to-be-released
block-scoped `stage` syntax in base Pipeline.
* _Required_: At least one is required.
* _Parameters_: A single `String`, the name for the `stage`.
* _Takes a Closure_: Yes
* _Closure Contents_:
** A `steps` block containing one or more Pipeline steps, including
block-scoped steps and the special `script` block described below, and
optionally, certain configuration sections that allow being set on a
per-stage basis.
*** _NOTE_: Only the "declarative subset" of Groovy is allowed by
default. See below for details on that subset.
*** _NOTE_: The `parallel` step is a special case - it can only be used
if it's the sole step in the `stage`.
** An `agent` section can be configured per-stage, see above.
** An optional `when` block specifying if the stage should run or not.
It can contain arbitrary Groovy code, but needs to return `true` if the
stage should run or `false` if not.
** An optional `post` block that runs after the steps in the stage. See
`post` below.
* _Examples_:

[pipeline]
----
// Declarative //
stages {
    stage('foo') {
        steps {
            echo 'bar'
        }
    }
}
----
[pipeline]
----
// Declarative //
stages {
    stage('first') {
        steps {
            timeout(time:5, unit:'MINUTES') {
                sh "mvn clean install -DskipTests"
            }
        }
    }

    stage('second') {
        agent {
            label 'some-node'
        }
        when {
            branch "master"
        }
        steps {
            checkout scm
            sh "mvn clean install"
        }
        post {
            always {
                email recipient: ['one@example.com','two@example.com'], subject: "Master Build complete", body: "Your build has completed"
            }
            failure {
                sh "bash ./cleanup-from-failure.sh"
            }
        }
    }
}
----
[pipeline]
----
// Declarative //
stages {
    stage('parallel-stage') {
        steps {
            parallel(
                firstBlock: {
                    echo "First block of the parallel"
                },
                secondBlock: {
                    echo "Second block of the parallel"
                }
            )
        }
    }
}
----

[[when]]
=== when

* _Description_: A block within a `stage` that determines whether the
stage should be executed.
* _Required_: No
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: One or more of the possible `when` conditions,
provided by an extension point.
** _Built-In `when` Conditions_:
*** `branch` - run when the branch being built matches the branch
pattern given.
*** `environment` - run when the environment contains a given
environment variable with a given value.
*** `expression` - run when an arbitrary Groovy expression returning a
boolean is true.
* _Examples_:

[pipeline]
----
// Declarative //
when { branch "*/master" }
----
[pipeline]
----
// Declarative //
when { environment name: "FOO", value: "SOME_OTHER_VALUE" }
----
[pipeline]
----
// Declarative //
when { expression { return "foo" == "bar" } }
----

[[script]]
=== script

* _Description_: A block within `steps` or `post` that can contain
Pipeline code not subject to the "declarative" subset described below.
* _Required_: No
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: Any valid Pipeline code.
* _Examples_:

[pipeline]
----
// Declarative //
image docker:'java:7'
stages {
    stage 'build' {
        steps {
            sh 'mvn install'
            script {
                // any valid Pipeline Script goes here
                def browsers = ["ie", "chrome", "safari"]
                for (int i = 0; i < browsers.size(); i++) {
                    def browser = browsers.get(i)
                    sh "./test.sh ${browser}"
                }
            }
        }
    }
}
----

[[tools]]
=== tools

* _Description_: A section defining tools to auto-install and put on the
PATH. This is ignored if `image none` is specified.
* _Required_: No
* _Allowed In_: Top-level `pipeline` or `stage` closures only.
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: Names and versions of tools configured in Jenkins
to install.
** Tool names are aliases to the `ToolDescriptor` class for that tool,
and must be one of a list of pre-configured possible tools. Currently
that's hardwired to just `maven`, `java`, and `gradle`, but this will be
changed to be an extensible system. The tool (and its version) must
already be configured on the Jenkins master in use.
** Tool versions are the names for specific tool installations
configured in Jenkins.
* _Examples_:

[pipeline]
----
// Declarative //
tools {
    maven "apache-maven-3.0.1"
    java "JDK 1.8"
}
----

[[post]]
=== post

* _Description_: Defines post-build actions to be run after build
completion, assuming build status conditions are met.
* _Required_: No
* _Allowed In_: Top-level `pipeline` closure only.
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: A sequence of one or more build conditions
containing Pipeline steps to run. See below for definition of build
conditions and their contents.

[[build-conditions]]
=== Build Conditions

* _Description_: Closures named for a particular build condition,
containing Pipeline steps to run if that condition is met.
* _Required_: One or more required in `post` if it exists.
* _Parameters_: None
* _Possible Condition Names_:
** Currently hardcoded, but will be changed to be extensible.
** `always`: Run regardless of build status.
** `aborted`: Run if build is aborted - note that this may not actually
work.
** `success`: Run if the build is successful (or more accurately, if the
build result hasn't been set to anything else).
** `unstable`: Run if the build result is unstable.
** `failure`: Run if the build failed.
** `changed`: Run if the build's result is different from the previous
build's result.
* _Takes a Closure_: Yes
* _Closure Contents_: A sequence of Pipeline steps, such as could be
included in a `stage`. Runs in an unspecified `node { ... }` block if
`image none` was specified.
* _Examples_:

[pipeline]
----
// Declarative //
post {
    always {
        email recipient: ['one@example.com','two@example.com'], subject: "Build complete", body: "Your build has completed"
    }
    failure {
        sh "bash ./cleanup-from-failure.sh"
    }
    success {
        sh "git push origin master"
    }
}
----

[[triggers]]
=== Triggers

* _Description_: Triggers for this job, as used in other Jenkins jobs.
* _Required_: No
* _Allowed In_: Top-level `pipeline` closure only.
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: A sequence of one or more trigger configurations,
using `@Symbol` names for constructors.
** Note that `[$class: 'Foo', arg1: 'something', ...]` syntax can not be
used, only `cron('@daily')` and the like.
** Also note that the `SCMTrigger` won't work with the `scm` `@Symbol` -
with Jenkins 2.22 or later, the `pollScm` symbol does work.
* _Examples_:

[pipeline]
----
// Declarative //
triggers {
    cron('@daily')
}
----

[[build-parameters]]
=== Build Parameters

* _Description_: Build parameters that will be prompted for at build
time.
* _Required_: No
* _Allowed In_: Top-level `pipeline` closure only.
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: A sequence of one or more parameter definition
configurations, using `@Symbol` names for constructors.
** Note that `[$class: 'Foo', arg1: 'something', ...]` syntax can not be
used, only `booleanParam(...)` and the like.
* _Examples_:

[pipeline]
----
// Declarative //
parameters {
    booleanParam(defaultValue: true, description: '', name: 'flag')
    string(defaultValue: '', description: '', name: 'SOME_STRING')
}
----

[[options]]
=== Options

* _Description_: Traditional `JobProperty`, such as `buildDiscarder` or
`disableConcurrentBuilds`, Declarative-specifc options, such as
`skipDefaultCheckout`, and "wrappers" that should wrap the entire build,
such as `timeout`.
* _Required_: No
* _Allowed In_: Top-level `pipeline` closure only.
* _Parameters_: None
* _Takes a Closure_: Yes
* _Closure Contents_: A sequence of one or more Declarative option or
job property configurations, using `@Symbol` names for constructors.
** Note that `[$class: 'Foo', arg1: 'something', ...]` syntax can not be
used, only `booleanParam(...)` and the like.
** Note that the `parameters` and `pipelineTriggers` `@Symbol` cannot
be used here directly.
* _Examples_:

[pipeline]
----
// Declarative //
options {
    buildDiscarder(logRotator(numToKeepStr:'1'))
    disableConcurrentBuilds()
}
----

[[declarative-subset-of-groovy]]
=== Declarative Subset of Groovy

* Top-level has to be a block
* No semicolons as statement separators. Each statement has to be on its
own line
* Block must only consists of method call statements, assignment
statements, or property reference statement
** A property reference statement is treated as no-arg method
invocation. So for example, `input` is treated as `input()`
* Expression has to be one of the following:
** Literals (except class literals)
** Numbers: `1`, `3`
** Booleans: `true`, `false`
** String literals regardless of their quotations: `"foo"`, `'bar'`
** Multi-line string literals
** Variable references: `x`
** Sequence of property references: `x.y.z`
** GString: `"hello ${exp}"`
** Literal list: `[exp,exp,...]`
** Literal map where keys are all constants: `[a:exp, b:exp, ... ]`
** Method calls where the left hand side is a variable reference or a
sequence of property references: `x.y.z(...)`
** Method calls (including `@Symbol` constructors like used above in
options, triggers and build parameters) where there is no left hand
side.
** Closure without parameters: `{ ... }`
